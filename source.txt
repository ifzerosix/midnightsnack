"Midnight Snack" by "I.F. Zerosix".
The story headline is "An Interactive Fiction". 
The story genre is "Slice of Life". 
The release number is 8.
The story description is "You awaken in the middle of the night craving a snack. The story can be won in as little as 30 moves, but one misstep along the way could result in a cascading comedy of errors and a night of insomnia."
The story creation year is 2023.
Release along with cover art, the introductory booklet, solution,introductory postcard, a website, the source text, a file of "License" called "License.txt" and an interpreter.

Volume 1 - Extensions

Include Bulk Limiter by Eric Eve.
Include Exit Lister by Eric Eve.
Include Underside by Eric Eve.

Volume 2 - Setup

Book 1 - Grammar

Use the serial comma.
Use American dialect.

Book 2 - Custom Kinds

Part 1 - Sleepwear

Sleepwear is a kind of thing. 
Sleepwear is wearable. 
Sleepwear can be wet or dry. 
Sleepwear is usually dry. 

Check wearing wet sleepwear:
	Say "Putting on [the noun] would be very uncomfortable.";
	Rule Fails.
	
Check wearing dry sleepwear when Player-Wet is True:
	Say "Why would you want to put on [the noun] when you are still wet from the shower?";
	Rule Fails.
	
Check Dropping something wet:
	Say "You can't leave [the noun] just lying here.";
	Rule Fails.

Check Inserting something wet into something:
	If the second noun is the dryer: 
		Continue the Action;
	Otherwise:
		If the Second Noun is the clothes basket:
			Continue the Action;
		Otherwise:
			If the second noun is the washing machine: 
				Continue the Action;
			Otherwise:
				Say "Perhaps [the second noun] isn't the best place to store [the noun].";
			Rule Fails.
			
Part 2 - Appliances
			
An appliance is a kind of container. 
An appliance is always scenery. 
An appliance is usually openable. 
An appliance is usually closed.

Check Switching On an appliance:
	Not-In-The-Mood-To-Cook;
	Rule Fails.
	
Check Switching Off an appliance:
	say "It is already off.";
	Rule Fails.
	
To Not-In-The-Mood-To-Cook:
	Let T be the Time of Day;
	Increase T by one minute;
	say "It is [T], and you are in no mood to cook.";
	Rule Fails.

Book 3 - Custom Rules

Part 1 - Fixed in Place Rule
	
To Dont-Rearrange:
	Say "Now is no time to rearrange the apartment."

Part 4 - Darkness Rules

Rule For Printing The Announcement of light: 
	say "Your surroundings spring to life, crisp and bright.";
	Try looking;
	Rule Succeeds.

Rule For Printing The Announcement of darkness: 
	say "Your surroundings fade away.";
	Try looking;
	Rule Succeeds.

Rule For Printing The Description of a dark room: say "Your eyes can barely make out anything." instead.

Check Going when in darkness:
	say "The last time you wandered around in the dark, you stubbed your toe.";
	Rule Fails.
	
Rule for printing a refusal to act in the dark: 
	say "Even though you are familiar with your apartment, it is certainly too dark for that.";
	Rule Fails.

Section 1 - Living Room
	
Rule For Printing The Description of a dark room when the location is the Living Room:
	say "It is pretty dark in this room, but you can make out the shadow of a [BeginHint]table lamp[EndHint] on an end table[If the woodgrain door is open and the bedroom lamp is lit]. You can see the bedroom lamp shining in your bedroom to the north[End If][If the kitchen light switch is lit]. The kitchen light is on to the south[End If][If the white door is open and the laundry light switch is lit]. The laundry room light is on to the east[End If]."
		
After Deciding the Scope of the Player when the location is the Living Room: 
	place the table lamp in scope;
	If the bedroom lamp is lit and the woodgrain door is open:
		place the woodgrain door in scope;
	If the laundry light switch is lit and the white door is open:
		place the white door in scope. 
	
Check Going north when the Player is in the Living Room and the table lamp is unlit and the bedroom lamp is lit and the woodgrain door is open:
	Say "";
	Now the Player is in the Bedroom;
	Rule Succeeds.
	
Check Going south when the Player is in the Living Room and the table lamp is unlit and the kitchen light switch is lit:
	Say "";
	Now the Player is in the Kitchen;
	Rule Succeeds.

Check Going east when the Player is in the Living Room and the table lamp is unlit and the laundry light switch is lit and the white door is open:
	Say "";
	Now the Player is in the Laundry Room;
	Rule Succeeds.

Section 2 - Kitchen

After Deciding the Scope of the Player when the location is the Kitchen: 
	place the kitchen light switch in scope. 

Rule For Printing The Description of a dark room when the location is the Kitchen:
	say "Your eyes can barely make out anything except the [BeginHint]kitchen light switch[EndHint][If the table lamp is lit]. The table lamp is on in the living room to the north[End If]."
	
Check Going north when the Player is in the Kitchen and the kitchen light switch is unlit and the table lamp is lit:
	Say "";
	Now the Player is in the Living Room;
	Rule Succeeds.

Check Going north when the Player is in the Kitchen and the kitchen light switch is unlit and the table lamp is unlit and the TV is lit:
	Say "";
	Now the Player is in the Living Room;
	Rule Succeeds.
	
Section 3 - Laundry Room
	
Rule For Printing The Description of a dark room when the location is the Laundry Room:
	say "Your eyes barely make out anything except the [BeginHint]laundry room light switch[EndHint][If the sliding door is open and the bathroom light switch is lit]. The bathroom light is on past an open sliding door to the north[End If][If the white door is open and the table lamp is lit]. To the west, through an open door, a table lamp glows on an end table in the living room[End If]."
	
After Deciding the Scope of the Player when the location is the Laundry Room: 
	place the laundry light switch in scope;
	If the table lamp is lit and the white door is open:
		place the white door in scope;
	If the bathroom light switch is lit and the sliding door is open:
		place the sliding door in scope.
	
Check Going north when the Player is in the Laundry Room and the laundry light switch is unlit and the bathroom switch is lit:
	Say "";
	Now the Player is in the En Suite Bathroom;
	Rule Succeeds.
	
Check Going west when the Player is in the Laundry Room and the laundry light switch is unlit and the table lamp is lit:
	Say "";
	Now the Player is in the Living Room;
	Rule Succeeds.
	
Check Going west when the Player is in the Laundry Room and the laundry light switch is unlit and the table lamp is unlit and the TV is lit:
	Say "";
	Now the Player is in the Living Room;
	Rule Succeeds.
	
Section 4 - En Suite Bathroom
	
Rule For Printing The Description of a dark room when the location is the En Suite Bathroom:
	say "Your eyes can barely make out anything except the [BeginHint]bathroom light switch[EndHint][If the sliding door is open and the laundry light switch is lit]. The laundry room light is on past an open sliding door to the south[End If][If the yellow door is open and the bedroom lamp is lit]. To the west, through an open door, a lamp glows on a nightstand in your bedroom[End If]."
	
After Deciding the Scope of the Player when the location is the En Suite Bathroom: 
	place the bathroom light switch in scope;
	If the bedroom lamp is lit and the yellow door is open:
		place the yellow door in scope;
	If the laundry light switch is lit and the sliding door is open:
		place the sliding door in scope.

Check Going west when the Player is in the En Suite Bathroom and the bathroom light switch is unlit and the bedroom lamp is lit:
	Say "";
	Now the Player is in the Your Bedroom;
	Rule Succeeds.
	
Check Going south when the Player is in the En Suite Bathroom and the bathroom light switch is unlit and the laundry light switch is lit:
	Say "";
	Now the Player is in the the Laundry Room;
	Rule Succeeds.
	
Section 5 - Your Bedroom

Rule For Printing The Description of a dark room when the location is Your Bedroom and the Player is in your bed:
	say "You lie wide awake in your bed[If Player-Hungry is True], starving[End If]. Your eyes can barely make out the [BeginHint]lamp[EndHint] on your nightstand[If the yellow door is open and the bathroom light switch is lit]. To the east, through an open yellow door, the light is on in your bathroom[End If][If the woodgrain door is open and the table lamp is lit]. The table lamp is on in the living room to the south[End If]."

Rule For Printing The Description of a dark room when the location is Your Bedroom:
	say "Your eyes can barely make out the [BeginHint]lamp[EndHint] on your nightstand[If the woodgrain door is open and the table lamp is lit]. The table lamp is on in the living room to the south[End If][If the yellow door is open and the bathroom light switch is lit]. To the east, through an open yellow door, the light is on in your bathroom[End If]."
	
After Deciding the Scope of the Player when the location is Your Bedroom: 
	place the bedroom lamp in scope;
	place the nightstand in scope;
	place your bed in scope;
	place the woodgrain door in scope;
	place the yellow door in scope.
	
Rule for Printing a Refusal to Act in the dark when Examining the bedroom lamp: 
	say "You can't examine the lamp closely because it is too dark, but you know it is there, and you could easily [BeginHint]switch on the lamp[EndHint].";
	Rule Succeeds.
	
Check Going south when the Player is in Your Bedroom and the bedroom lamp is unlit and the table lamp is lit:
	Say "";
	Now the Player is in the the Living Room;
	Rule Succeeds.

Check Going south when the Player is in Your Bedroom and the bedroom lamp is unlit and the table lamp is unlit and the TV is lit:
	Say "";
	Now the Player is in the the Living Room;
	Rule Succeeds.
	
Check Going east when the Player is in Your Bedroom and the bedroom lamp is unlit and the bathroom light switch is lit:	Say "";
	Now the Player is in the En Suite Bathroom;
	Rule Succeeds.

Book 4 - Custom Actions


Part 1 - Looking Out

Understand "look out [something]" and "look out of [something]" as Examining.

Part 2 - Getting Out Of and Getting Back Into

Getting Out Of It is an action applying to one thing. Understand "get out of [something]" and "get out of the [something]" and "get out of my [something]" as Getting Out Of It.

Check Getting Out Of It:
	Try Exiting instead;
	Rule Succeeds.
	
Getting Back Into It is an action applying to one thing.
Understand "get back in [something]" and "get back in the [something]" and "get back in my [something]" and "get back into [something]" and "get back into the [something]" and "get back into my [something]" as Getting Back Into It.

Check Getting Back Into It:
	Try Entering the noun instead;
	Rule Succeeds.
	
Check Getting Out Of It:
	Try Exiting instead;
	Rule Succeeds.
	
Part 3 - Using the Toilet

Chapter 1 - Going Potty

Going Potty is an action applying to nothing. 
Understand "go potty", "poop", "defecate", "crap", "take a crap", "pee", "urinate", "go number 1", "go number one", "go number 2", "go number two", and "drop a deuce" as Going Potty.

Carry Out Going Potty:
	say "Here? I don't think so.";
	Rule Fails.
	
Check Going Potty when the Player is in the En Suite Bathroom:
	say "You went earlier.";
	Rule Fails.
	
Chapter 2 - Flush the Toilet

Flushing is an action applying to one thing and requiring light. 
Understand "flush [something]" as Flushing.

Carry Out Flushing:
	say "You can't flush [the noun].";
	Rule Fails.
	
Part 6 - Direction-Looking

Direction-Looking is an action applying to one visible thing[ and requiring light]. 
Understand "look [direction]" as Direction-Looking.

Carry out Direction-Looking:
	say "You see nothing unusual in that direction.";
	Rule Succeeds.

Section 1 - Your Bedroom
	
Check Direction-Looking east when the Player is in Your Bedroom:
	say "Your bathroom lies[If the bathroom light switch is unlit] in darkness[End If][If the yellow door is closed] beyond the door[End If] to the east.";
	Rule Succeeds.
	
Check Direction-Looking south when the Player is in Your Bedroom:
	say "The living room lies[If the table lamp is unlit and the TV is unlit] in darkness[End If][If the woodgrain door is closed] beyond the door[End If] to the south.";
	Rule Succeeds.
	
Section 2 - Living Room

Check Direction-Looking north when the Player is in the Living Room:
	say "Your bedroom lies[If the bedroom lamp is unlit] in darkness[End If][If the woodgrain door is closed] beyond the door[End If] to the north.";
	Rule Succeeds.
	
Check Direction-Looking south when the Player is in the Living Room:
	say "The kitchen lies[If the kitchen light switch is unlit and the refrigerator is unlit] in darkness[End If] to the south.";
	Rule Succeeds.
	
Check Direction-Looking east when the Player is in the Living Room:
	say "The laundry room lies[If the laundry light switch is unlit] in darkness[End If][If the white door is closed] beyond the door[End If] to the east.";
	Rule Succeeds.
	
Section 3 - Kitchen

Check Direction-Looking north when the Player is in the Kitchen:
	say "The living room lies[If the table lamp is unlit and the TV is unlit] in darkness[End If] to the north.";
	Rule Succeeds.
	
Section 4 - Laundry Room

Check Direction-Looking north when the Player is in the Laundry Room:
	say "The bathroom lies[If the bathroom light switch is unlit] in darkness[End If][If the sliding door is closed] beyond the sliding door[End If] to the north.";
	Rule Succeeds.
	
Check Direction-Looking west when the Player is in the Laundry Room:
	say "The living room lies[If the table lamp is unlit and the TV is unlit] in darkness[End If][If the white door is closed] beyond the door[End If] to the west.";
	Rule Succeeds.
	
Section 5 - En Suite Bathroom

Check Direction-Looking south when the Player is in the En Suite Bathroom and the sliding door is open:
	say "The laundry room lies[If the laundry light switch is unlit] in darkness[End If][If the sliding door is closed] beyond the door[End If] to the south.";
	Rule Succeeds.
	
Check Direction-Looking west when the Player is in the En Suite Bathroom and the yellow door is open:
	say "Your bedroom lies[If the bedroom lamp is unlit] in darkness[End If][If the yellow door is closed] beyond the door[End If] to the west.";
	Rule Succeeds.
	
Part 7 - Washing It

Washing It is an action applying to one visible thing and requiring light. 
Understand "wash [something]" as Washing It.

Chapter 1 - Default Washing It

Carry out Washing It:
	Say "You can't wash [the noun] here.";
	Rule Fails.

Chapter 2 - Washing It in the Laundry Room

Check Washing It when the Player is in the Laundry Room:
	If the noun is wearable:
		Let T be the Time of Day;
		Increase T by one minute;
		Say "It is [T]. There are better times to do laundry.";
	Otherwise:
		Say "You can't wash [the noun] here.";
	Rule Fails.

Chapter 3 - Washing It in the Kitchen
	
Check Washing It when the Player is in the Kitchen:
	If the noun is the dishes:
		Let T be the Time of Day;
		Increase T by one minute;
		say "It is [T]. There are better times to do the dishes.";
	Otherwise:
		If the noun is the butter knife:
			Let T be the Time of Day;
			Increase T by one minute;
			say "It is [T]. There are better times to do the dishes.";				
		Otherwise:
			say "The sink is full of dirty dishes. It might not be the best place to wash [the noun].";
	Rule Fails.

Chapter 4 - Washing It in the Bathroom
	
Check Washing It when the Player is in the En Suite Bathroom and the noun is the Player:
	If the sink faucets are Switched On:
		say "You wash up, using the bathroom sink.";
		Now the b#it is nowhere;
		Now the dol#lop is nowhere;
		Now Player-Dirty is False;
		Rule Succeeds;
	Otherwise:
		If the shower-faucets are Switched On:
			Try Entering the shower;
			Rule Succeeds;
		Otherwise:
			say "You will either need to [BeginHint]turn on the sink or get in the shower[EndHint].";
		Rule Fails.

	
Part 8 - Spreading It On

Spreading It On is an action applying to two things and requiring light. 
Understand "spread [something] on [something]" and "spread [something] onto [something]" as Spreading It On.

Carry Out Spreading It On:
	Try Putting the noun on the second noun;
	Rule Fails.
	
Check Inserting the butter knife into the jelly:
	If the Player encloses the bread:
		Try Putting the jelly on the bread;
		Rule Succeeds;
	Otherwise:
		Say "You aren't holding anything to spread the jelly on.";
		Rule Succeeds.
	
Check Inserting the butter knife into the peanut butter:
	If the Player encloses the bread:
		Try Putting the peanut butter on the bread;
		Rule Succeeds;
	Otherwise:
		Say "You aren't holding anything to spread the peanut butter on.";
		Rule Succeeds.

Part 9 - Taking Something While Dirty

Check Taking something when the Player does not enclose the noun and Player-Dirty is True:
	Say "You had better not touch [the noun] with dirty fingers.";
	Rule Fails.
	
Part 8 - Pointing It At

Pointing It At is an action applying to two things.
Understand "point [something] at [something]" and "wave [something] at [something]" as Pointing It At. 
	
Report Pointing something at something:
	If the Player carries the noun:
		Say "You point [the noun] at [the second noun]. Nothing happens.";
	Otherwise:
		Say "You can't point [the noun] if you are not holding [the noun].";
	Rule Fails.
	
Part 9 - Drying It

Drying It is an action applying to one visible thing and requiring light. 
Understand "dry off", "dry [something]", "dry [something]off", "dry off [something]", "dry [something] with the towel", "dry [something] off with the towel", "dry off [something] with the towel","dry [something] with towel" as Drying It.

Report Drying It:
	If the noun is the Player:
		Say "If you want to dry [the noun], you need a dry towel.";
		Rule Fails;
	Otherwise:
		If the noun is something wearable:
			Say "If you want to dry [the noun], you could Try the clothes dryer.";
			Rule Fails;
		Otherwise:
			Say "I don't understand why you would want to dry [the noun]. Is [the noun] even wet?";
			Rule Fails;
	
Part 10 - Help
	
Help is an action out of world applying to nothing. 
Understand "help" as help.

Carry Out Help:
	Say "In interactive fiction, you type basic commands that the game translates into character actions.[Paragraph Break][Bold Type]Verb + Noun[Roman Type][Line Break]Verb and noun combinations are the simplest and most frequently used commands. These include actions like: [Bold Type]go north[Roman Type], [Bold Type]turn on the shower[Roman Type], or [Bold Type]examine the sandwich[Roman Type].[Paragraph Break][Bold Type]Verb + Noun + Second Noun[Roman Type][Line Break]Verb, noun, and second noun combinations allow two different objects to interact with one another. These include actions like: [Bold Type]spread the jelly on the bread[Roman Type], [Bold Type]point the remote control at the TV[Roman Type], or [Bold Type]unlock the blue door with the blue key[Roman Type].[Paragraph Break][Bold Type]Inventory[Roman Type][Line Break]You can see what you are wearing and carrying by typing [Bold Type]inventory[Roman Type] or [Bold Type]i[Roman Type]. In this game, the maximum number of things you can carry is [Bold Type]4[Roman Type], unless you find a portable container that allows you to carry more things.[Paragraph Break][Bold Type]Out-of-Game Commands[Roman Type][Line Break]Type [Bold Type]save[Roman Type] or [Bold Type]restore[Roman Type] to save or restore your progress. Toggle the hint system on and off by typing [Bold Type]hints[Roman Type]. When the hint system is active, clues will appear in [Bold Type]bold[Roman Type] in the game text.[Paragraph Break]Forgot where you are? Try typing [Bold Type]look[Roman Type] or [Bold Type]l[Roman Type].";
	Rule Fails.

Part 11 - Hints

Hints is an action out of world applying to nothing. 
Understand "hints","hint","hints on","hints off" as Hints.

Hints-On is a Truth State that varies. Hints-On is False.
	
To say BeginHint:
	if Hints-On is True:
		say "[Bold Type]".

To say EndHint:
	If Hints-On is True:
		say "[Roman Type]".
	
Carry out Hints:
	If Hints-On is False:
		Now Hints-On is True;
		Say "Hints now appear in bold. Type [Bold Type]hints[Roman Type] again to toggle hints off.[Paragraph Break]Forgot where you are? Try typing [Bold Type]look[Roman Type] or [Bold Type]l[Roman Type].";
	Otherwise:
		Now Hints-On is False;
		Say "Hints will no longer appear in bold. Type [Bold Type]hints[Roman Type] again to toggle hints on.";
	Rule Succeeds.

Part 12 - Inventory

Inventory is an action out of world.
	
Book 6 - Truth States

Part 1 - Player-Hungry

Player-Hungry is a Truth State that varies. 
Player-Hungry is True.

Part 2 - Player-Dirty

Player-Dirty is a Truth State that varies. 
Player-Dirty is False.

Part 3 - Sitting-Down

Sitting-Down is a Truth State that Varies. 
Sitting-Down is False.

Part 4 - Too-Much-TV

Too-Much-TV is a Truth State that Varies. 
Too-Much-TV is False.
	
Part 5 - Player-Wet
	
Player-Wet is a Truth State that varies. 
Player-Wet is False.

Part 6 - Knife-Dirty

Knife-Dirty is a Truth State that varies. 
Knife-Dirty is False.

Volume 3 - The Player

The Player is in your bed. 

Understand "face", "hair", "hands", "hand", "feet", "body","my body", "finger", "fingers" as Yourself.

The carrying capacity of the Player is 4.

The description of the Player is "[If Player-Wet is True]You are all wet. [End If]You wear [a list of things worn by the Player]. You carry [a list of things carried by the Player]."

Check Examining the bathroom mirror:
	say "You see your reflection. [Run Paragraph On]";
	Try Examining the Player;
	Rule Succeeds.

Book 1 - Going Back to Sleep

Check sleeping when the Player is not in Your Bed:
	say "Maybe if you were in bed.";
	Rule Fails.
	
Check sleeping when Player-Hungry is True:
	say "You are way too hungry to sleep.";
	Rule Fails.
	
Check sleeping when Player-Hungry is False and the Player is in Your Bed and the bedroom lamp is lit:
	say "You will never fall asleep with that lamp on.";
	Rule Fails.
	
Book 2 - Sleepwear

Your favorite comfy PJs is sleepwear. 
Your favorite comfy PJs are plural-named.
Understand "pajamas","jammies","jam jams" as your favorite comfy PJs.
	
The Player is wearing your favorite comfy PJs. 

Check wearing sleepwear when the Player wears your cotton robe:
	say "You have to take off your robe first.";
	Rule Fails.

Check wearing sleepwear when the Player wears sleepwear:
	say "You have to take off what you are wearing first.";
	Rule Fails.
	
Check wearing sleepwear when the Player wears the towel:
	say "You have to take off the towel first.";
	Rule Fails.
	
Check Taking off sleepwear when the Player wears your cotton robe:
	say "You have to take off your robe first.";
	Rule Fails.

Volume 4 - The Game

When play begins: 
	now the Time of Day is 12:00 am;
	now the right hand status line is "[Time of Day]";
	say "You just woke up in the middle of the night, starving. There is no way you are Going back to sleep without a snack.[Paragraph Break]Type [Bold Type]help[Roman Type] if you need help."

Book 1 - Rooms and Regions

Part 1 - Regions
[No Regions]

Part 2 - Your Bedroom

Your Bedroom is a room. 
Your Bedroom is dark. 
The description is "[If the Player is enclosed by the bed]You are lying in your bed in your apartment. [End If]A lamp shines on a nightstand next to your bed. A highboy dresser stands against one wall, and a closet door stands [if the bedroom closet is open]open[otherwise]closed[End If] on the other. The curtains are drawn on a window overlooking the street below.[Paragraph Break][If the woodgrain door is open]An open[otherwise]A closed[End If] woodgrain door to the [BeginHint]south[EndHint] leads out into your apartment's living room. [If the yellow door is open]An open[otherwise]A closed[End If] yellow door to the east leads to your bathroom."

The woodgrain door is a scenery door. 
The woodgrain door is open. 
The woodgrain door is south of Your Bedroom.

Chapter 1 - Your Bed

Your bed is in Your Bedroom. 
Your bed is scenery. 
Your bed is a container. 
Your bed is enterable. 
Your bed is transparent. 
The description is â€œSleeping on this ten-inch-thick California King is as close to floating on air as you will ever get."

Check Entering your bed when the Player carries something:
	say "You can't climb into bed while you are carrying [a list of things carried by the Player]!";
	Rule Fails.
	
Check Entering your bed when the Player wears something wet:
	say "You can't climb into bed while wearing [a list of wet things worn by the Player]!";
	Rule Fails.
		
Check Entering your bed when Player-Dirty is True:
	say "You should probably get washed up first.";
	Rule Fails.
	
Check Entering your bed when the Player is not wearing sleepwear:
	say "If you don't put on some pajamas, you will never be able to fall asleep.";
	Rule Fails.

Check Entering your bed when Player-Hungry is True:
	say "You climb back into bed, but you will never fall asleep as hungry as you are.";
	now the Player is in your bed;
	Rule Succeeds.

Chapter 2 - Nightstand

The nightstand is a scenery supporter in Your Bedroom.
The nightstand is fixed in place. 
Understand "table" as the nightstand.
The description of the nightstand is "It is just an ordinary nightstand, basically a table with a lamp on it."  
 
Chapter 3 - Bedroom Lamp

The bedroom lamp is a device in Your Bedroom. 
The bedroom lamp is scenery.   
Understand "light", "shade", "lamp shade", "lampshde" as the bedroom lamp.
The description of the bedroom lamp is "It is one of the first things you purchased when you moved in. It is also the last thing you turn off before Going to sleep."

Check Switching On the bedroom lamp:
	now the bedroom lamp is lit;
	Continue the Action.
	
Check Switching Off the bedroom lamp:
	now the bedroom lamp is unlit;
	Continue the Action.
		
Chapter 4 - Highboy Dresser

The highboy dresser is a container in your bedroom. 
The highboy dresser is scenery. 
The highboy dresser is openable. 
The highboy dresser is closed.  
Understand "drawer", "bureau", "chest", "highboy", "top drawer", "bottom drawer", "middle drawer" and "drawers" as the highboy dresser.
The description is "It is a typical free-standing dresser. The dresser's drawers are [If the dresser is open]open[otherwise]closed[End If]." 

Your folded clothes are in the dresser. 
The folded clothes are plural-named. 
The description is "Your t-shirts, underwear, socks, and a bunch of other clothes are all folded neatly in your dresser[If the sweats are nowhere and Hints-On is True]. [BeginHint]If you are looking for something specific, you could Try Searching the clothes[EndHint][End If][If the sweats are in the highboy dresser]. Some sweats lie among your folded clothes[End If]."

Check Searching your folded clothes when the sweats are nowhere:
	Now the sweats are in the dresser;
	Say "You find a sweatshirt and sweatpants in your folded clothes.";
	Rule Succeeds.
	
Your sweats are sleepwear. 
Your sweats are nowhere. 
The printed name of your sweats is "a sweatshirt and sweatpants". 
Understand "sweatpants", "sweat pants", "sweatshirt", "sweat shirt", "sweatshirt and sweatpants", "sweatpants and sweatshirt", "sweat shirt and sweat pants", "sweat pants and sweat shirt", "shirt", and "pants" as your sweats. 
The description is "This pair of sweats includes an old crew neck sweatshirt and matching open-hem cotton pants. Sometimes, you lounge around the apartment in them."

Check Taking your folded clothes:
	say "Let's be real. You can't carry around all your clothes.";
	Rule Fails.

Check Opening something when the Player is enclosed by your bed:
	say "You can't open [the noun] until you get out of bed.";
	Rule Fails.
	
Check Closing something when the Player is enclosed by your bed:
	say "You can't close [the noun] until you get out of bed.";
	Rule Fails.
	
Check Taking something when the Player is enclosed by your bed and the noun is not enclosed by your bed:
	say "You can't take [the noun] until you get out of bed.";
	Rule Fails.
		
Check Taking your sweats when the Player does not enclose the sweats and the Player carries less than 4 things:
	say "You take the sweatshirt and sweatpants.";
	Now the printed name of the sweats is "your sweats";
	Now the sweats are plural-named;
	Now the Player carries the sweats;
	Rule Succeeds.
	
Chapter 5 - Bedroom Closet

The bedroom closet is in Your Bedroom. 
The bedroom closet is a container. 
The bedroom closet is openable. 
The bedroom closet is closed. 
The bedroom closet is scenery. 
The bedroom closet can be searched or unsearched. 
The bedroom closet is unsearched.

The closet door is part of the bedroom closet.

Your hanging clothes are in the bedroom closet. 
Your hanging clothes are plural-named. 
The description is "Your shirts, pants, and other nice attire all hang neatly in your closet[If your cotton robe is nowhere and Hints-On is True]. [BeginHint]If you are looking for something specific, you could Try Searching the clothes[EndHint][End If][If your robe is in the bedroom closet]. Your robe hangs among your clothes[End If]."

Check Searching the hanging clothes when the cotton robe is nowhere:
	Now the your cotton robe is in the bedroom closet;
	Say "You find your cotton robe in the closet.";
	Rule Succeeds.

Your cotton robe is a thing. 
Your cotton robe is wearable. 
Your cotton robe can be wet or dry. 
Your cotton robe is dry. 
Your cotton robe is nowhere. 
The description is "This full-length, hooded, fleece robe was a birthday present."
	
Check Opening the closet door:
	Try Opening the bedroom closet;
	Rule Succeeds.
	
Check Closing the closet door:
	Try Closing the bedroom closet;
	Rule Succeeds.
	
Check Taking your hanging clothes:
	say "Let's be real. You can't carry around all your clothes.";
	Rule Fails.
	
Check Entering the Bedroom Closet:
	say "There is not enough room for both you and your clothes. Even if there were, what would be the point?";
	Rule Fails.
	
Check Going west in Your Bedroom when the bedroom lamp is lit:
	if the bedroom closet is closed:
		say "First thing is first, you open the closet door.";
		now the bedroom closet is open;
		Try Entering the bedroom closet;
	otherwise:
		Try Entering the bedroom closet;
	Rule Succeeds.
	
Part 3 - Living Room

The Living Room is a room. 
The Living Room is south of the woodgrain door. 
The living room is dark. 
The description of the Living Room is "Your living room is modestly furnished with a couch, chair, TV, and an end table. A lamp on the end table [If the table lamp is lit]provides light[Otherwise]is Switched Off[End If][If the TV is Switched On], while the TV plays a late night show[End If]. Some curtains are drawn on a window overlooking the street below.[Paragraph Break][If the woodgrain door is open]An open[otherwise]A closed[End If] woodgrain door to the north leads to your bedroom, the kitchen lies to the [BeginHint]south[EndHint], and [If the white door is open]an open[otherwise]a closed[End If] white door to the east leads to the laundry room."

Chapter 1 - Couch

A couch is a scenery supporter in the living room.
Understand "sofa" as the couch.
The description of the couch is "It is a three-cushion seat with a padded back and arms."

Check Entering the couch:
	say "You sit down on the couch.";
	Now the Player is on the couch;
	Now Sitting-Down is True;
	Rule Succeeds.

Check Exiting when the Player is on the couch:
	Now Sitting-Down is False;
	Continue the Action.
	
Chapter 2 - Chair

A chair is scenery supporter in the living room.
The description of the chair is "It is an armless chair with a single-cushion seat and a padded back[If Hints-On is True]. [BeginHint]In the past, you have lost a few things under this chair after they slid off[EndHint][End If]."

An underside called under#chair is part of the chair.

Check Entering the chair:
	say "You sit down on the chair.";
	Now the Player is on the chair;
	Now Sitting-Down is True;
	Rule Succeeds.
	
Check Exiting when the Player is on the chair:
	Now Sitting-Down is False;
	Continue the Action.
	
Chapter 3 - TV

The TV is a device in the living room. 
The TV is Switched Off. 
The TV is scenery. 
Understand "television" as the TV.
The description of the TV is "It is an eighty-two-inch, ultra-high-def TV."

The remote control is in under#chair. 
Understand "button" as the remote control. 
The description is "This is the remote control for the TV. While you are holding it, you can turn the TV on and off."

Report Pointing the remote control at the TV when the Player carries the remote control:
	If the TV is Switched Off:
		Try Switching On the TV;
	Otherwise:
		Try Switching Off the TV;
	Rule Succeeds.
	
Check Pushing the remote control when the Player carries the remote control:
	If the TV is Switched Off:
		Try Switching On the TV;
	Otherwise:
		Try Switching Off the TV;
	Rule Succeeds.	
	
Check Switching On the TV when the Player does not carry the remote control:
	say "You are not holding the remote control.";
	Rule Fails.
	
Check Switching Off the TV when the Player does not carry the remote control:
	say "You are not holding the remote control.";
	Rule Fails.

Check Switching On the TV:
	Now the TV is lit;
	Continue the Action.

Check Switching Off the TV:
	Now the TV is unlit;
	Continue the Action.

Section 1 - Watching TV

Check Examining the TV when the TV is Switched On and Sitting-Down is True and Too-Much-TV is False:
	now the Time of Day is 29 minutes after the Time of Day;
		say "You watch TV for half an hour.";
		Now Dryer-Timer is 0;
		Watch-TV;
	Rule Succeeds.
	
Check Examining the TV when the TV is Switched On and Sitting-Down is True and Too-Much-TV is False for the second time:
	now the Time of Day is 29 minutes after the Time of Day;
		say "You watch TV for half an hour.";
		Now Dryer-Timer is 0;
		Now Too-Much-TV is True;
		Watch-TV;
	Rule Succeeds.
	
To Watch-TV:
	Let Z be 30;
	While Z is not 0:
		Increment Turn Count;
		Decrease Z by 1.
	
Check Examining the TV when the TV is Switched On and Sitting-Down is False:
	say "If you want to watch TV, you might be more comfortable sitting down.";
	Rule Fails.
	
Check Examining the TV when the TV is Switched On and Sitting-Down is True and Too-Much-TV is True:
	say "You have had enough TV for one night. You need to get back to sleep.";
	Rule Fails.

Chapter 4 - Lamp and End Table

The end table is a scenery supporter in the living room. 

The table lamp is a scenery device on the end table. 
The table lamp is Switched Off. 
Understand "light" as the table lamp.

Section 1 - Living Room Lamp

Check Switching On the table lamp:
	Now the table lamp is lit;
	Continue the Action.
	
Check Switching Off the table lamp:
	Now the table lamp is unlit;
	Continue the Action.

Part 4 - En Suite Bathroom

The En Suite Bathroom is a room. 
The En Suite Bathroom is north of the sliding door and east of the yellow door. 
The En Suite Bathroom is dark. 
The description is "The en suite bathroom is where you start and finish each day. The washroom contains a shower[if the shower-faucets are Switched On], which is running right now[End If], a pedestal sink[if the sink faucets are Switched On] with water pouring out of the faucets[End If], [if the medicine cabinet is open]an open[Otherwise]a closed[End If] medicine cabinet, and the toilet. A light switch on the bathroom wall is switched [if the bathroom light switch is Switched On]on[otherwise]off[End If]. [paragraph break][If the sliding door is open]An open[otherwise]A closed[End If] sliding door to the south leads to the laundry room. [If the yellow door is open]An open[otherwise]A closed[End If] yellow door to the west leads to your bedroom."

The yellow door is a scenery door. 
The yellow door is open. 
The yellow door is east of Your Bedroom.

Chapter 1 - Bath Towel
			
A bath towel is in the En Suite Bathroom. "A bath towel hangs outside the shower." 
The bath towel can be either wet or dry. 
The bath towel is dry. 
The bath towel is wearable. 
The description of the bath towel is "It is a [If the bath towel is dry]dry[Otherwise]wet[End If] bath towel[If Player-Wet is True and the bath towel is dry]. You could use it to dry yourself[End If][If Player-Wet is True and the bath towel is wet]. You can't dry yourself off with a wet bath towel[End If]."

Check Taking the bath towel when the Player does not enclose the bath towel and Player-Wet is True and the Player wears something:
	Say "If you want to dry off, you will need to [BeginHint]take off [a list of things worn by the Player][EndHint].";
	Rule Fails.
	
Check Taking the wet bath towel when the Player does not enclose the bath towel and Player-Wet is True and the Player carries less than 4 things:
	Say "You take the bath towel. It is all wet! You can't dry yourself off with this.";
	Now the Player carries the bath towel;
	Rule Fails.

Check Taking the dry bath towel when the Player does not enclose the bath towel and Player-Wet is True and the Player wears nothing and the Player carries less than 4 things:
	Say "You take the bath towel and dry yourself off. Now, the bath towel is wet, but at least you are dry.";
	Now the Player carries the bath towel;
	Now Player-Wet is False;
	Now the bath towel is wet;
	Now the printed name of the bath towel is "wet bath towel";
	Rule Succeeds.
	
Check Taking the dry bath towel when the Player does not enclose the bath towel and Player-Wet is True and the Player wears nothing and the Player carries less than 4 things and the Player is in the shower and the shower-faucets are Switched On:
	Say "While the shower is still running, you reach out from the shower and take the bath towel. Now the towel is all wet.";
	Now the Player carries the bath towel;
	Now the bath towel is wet;
	Now the printed name of the bath towel is "wet bath towel";
	Rule Succeeds.
	
Check wearing the bath towel when the Player does not wear the bath towel and the Player wears something:
	Say "You can't put on [the noun] when you are already wearing [a list of things worn by the Player].";
	Rule Fails.

Chapter 2 - Bathroom Light Switch

The bathroom light switch is a device in the En Suite Bathroom. 
The bathroom light switch is scenery.
Understand "light" as the bathroom light switch.

Check Switching On the bathroom light switch:
	Now the bathroom light switch is lit;
	Continue the Action.
	
Check Switching Off the bathroom light switch:
	Now the bathroom light switch is unlit;
	Continue the Action.
	
Chapter 3 - Shower

The shower is a container in the En Suite Bathroom. 
The shower is enterable. 
The shower is scenery. 
The shower is transparent.
Understand "showerhead", "shower head" as the shower. 
The description is "You [BeginHint]wash yourself[EndHint] almost everyday in this nice tiled shower[If the shower-faucets are Switched On], and warm water currently sprays from the showerhead[End If]."

Some shower-faucets are a device. 
The shower-faucets are scenery. 
The shower-faucets are in the En Suite Bathroom. 
Understand "shower faucet", "showerhead", "shower head", and "faucet" as the shower-faucets.

Check Washing It when the Player is in the shower and the shower-faucets are Switched On:
	say "You wash up.";
	Rule Succeeds.

Check Switching On the shower:
	Try Switching On the shower-faucets;
	Rule Succeeds.
	
Check Switching Off the shower:
	Try Switching Off the shower-faucets;
	Rule Succeeds.
	
Check Switching On the shower-faucets when the shower-faucets are Switched Off and the Player is not enclosed by the shower:
	say "You turn on the shower, and water sprays from the shower head.";
	Now the shower-faucets are Switched On;
	Now some water is in the shower;
	Rule Succeeds.
	
Check Switching Off the shower-faucets when the shower-faucets are Switched On:
	say "You turn off the shower, and water stops spraying from the shower head.";
	Now the shower-faucets are Switched Off;
	If the sink faucets are Switched On:
		Now some water is in the en suite bathroom;
	Otherwise:
		Now some water is nowhere;
	Rule Succeeds.

Check looking when the Player is enclosed by the shower:
	say "[bold type]En Suite Bathroom[Roman type] (in the shower) [paragraph break]";
	say "You are standing in a tiled shower[If the shower-faucets are Switched On and the Player carries something] with warm water spraying down on you and [a list of things carried by the Player][Otherwise if the shower-faucets are Switched On] with warm water spraying down on you[End If].";
	Rule Succeeds.
	
Check Dropping something when the Player is enclosed by the shower:
	say "Best not to leave [those] here.";
	Rule Fails.
	
Section 1 - Entering the Shower with the Shower On
	
Check Entering the shower when the shower-faucets are Switched On and the Player carries the PBJ sandwich:
	End the story finally saying "The shower ruined your sandwich. You are Going to be starving all night.".

Check Entering the shower when the shower-faucets are Switched On:
	Say "You get into the shower.[Paragraph Break]";
	Soak-The-Clothes;
	Now the Player is in the shower;
	Rule Succeeds.
	
Section 2 - Turning on the Shower While Standing In it
	
Check Switching On the shower-faucets when the Player is enclosed by the shower and the Player carries the PBJ sandwich:
	End the story finally saying "The shower ruined your sandwich. You are Going to be starving all night.".
	
Check Switching On the shower-faucets when the Player is enclosed by the shower:
	Now the shower-faucets are Switched On;
	say "You turn on the shower.[Paragraph Break]";
	Now the shower-faucets are Switched On;
	Now some water is in the shower;
	Soak-The-Clothes;
	Try Looking;
	Rule succeeds.		
	
Section 3 - Soaking the Clothes
	
To Soak-The-Clothes:
	Now Player-Dirty is False;
	Now Player-Wet is True;
	Now the b#it is nowhere;
	Now the dol#lop is nowhere;
	If the Player carries the butter knife:
		Now Knife-Dirty is False;
		Now the printed name of the butter knife is "butter knife";
	If the Player wears your favorite comfy PJs:
		Now the printed name of your favorite comfy PJs is "your soaking wet PJs";
	If the Player wears the sweats:
		Now the printed name of the sweats is "your soaking wet sweats";
	If the Player wears your robe:
		Now the printed name of your robe is "your soaking wet robe";
	If the Player wears the bath towel:
		Now the printed name of the bath towel is "soaking wet bath towel";
	repeat with item running through things worn by the Player:
		now item is wet.	
		
Chapter 4 - Bathroom Mirror

The bathroom mirror is in the En Suite Bathroom. 
The bathroom mirror is scenery.

Check Searching the bathroom mirror:
	Try Examining the bathroom mirror;
	Rule Succeeds.

Check Examining the bathroom mirror when the medicine cabinet is open:
	say "You will need to close the medicine cabinet first.";
	Rule Fails.

Check Examining the bathroom mirror:
	say "You see your reflection[If Player-Wet is True]. You are all wet[End If]. [run paragraph on]";
	Try Examining the Player;
	Rule Succeeds.
	
Chapter 5 - Pedestal Sink

The pedestal sink is in the En Suite Bathroom. 
The pedestal sink is scenery. 
The description is "It is a pedestal sink with a rectangular basin. Typically, you [BeginHint]wash your hands[EndHint] here. The sink faucets are currently turned [if the sink faucets are Switched Off]off[Otherwise]on[End If]." 

Some sink faucets are a device. 
The sink faucets are part of the pedestal sink. 
Understand "sink faucet" and "faucet" as the sink faucets. 

Check Examining the sink faucets:
	say "The sink faucets are currently turned [if the sink faucets are Switched Off]off[Otherwise]on[End If].";
	Rule Succeeds.
	
Check Switching On the pedestal sink:
	Try Switching On the sink faucets;
	Rule Succeeds.
	
Check Switching Off the pedestal sink:
	Try Switching Off the sink faucets;
	Rule Succeeds.
	
Check Switching On the sink faucets when the sink faucets are Switched Off:
	say "You turn on the sink faucets, and water pours out of the spout.";
	Now the sink faucets are Switched On;
	Now some water is in the en suite bathroom;
	Rule Succeeds.
	
Check Switching Off the sink faucets when the sink faucets are Switched On:
	say "You turn off the sink faucets, and water stops pouting out of the spout.";
	Now the sink faucets are Switched Off;
	If the shower-faucets are Switched On:
		Now some water is in the shower;
	Otherwise:
		Now some water is nowhere;
	Rule Succeeds.
	
Chapter 6 - Toilet
	
The toilet is in the En Suite Bathroom. 
The toilet is scenery.

Carry Out Flushing the toilet:
	say "You flush [the noun].";
	Rule Succeeds.
	
Chapter 7 - Medicine Cabinet

The medicine cabinet is a container in the En Suite Bathroom. 
The medicine cabinet is scenery. 
The medicine cabinet is openable. 
The medicine cabinet is closed. 
Understand "med cabinet","med" as the medicine cabinet.
The description of the medicine cabinet is "The medicine cabinet hangs over the sink. The cabinet is currently [if the medicine cabinet is open]open[Otherwise]closed, and you can see yourself in the mirror on the door[End If]."

Section 1 - Laxative

A laxative is in the medicine cabinet. 
Understand "lax" as the laxative. 
The description of the laxative is "'Stay near a bathroom after Taking.' Sound advice, and a good place to store it, too, as it turns out."

Check Taking the laxative:
	say "I don't think that is Going to solve your hunger problem. Leave it here.";
	Rule Fails.

Section 2 - Toothbrush

A toothbrush is in the medicine cabinet. 
Understand "brush" as the toothbrush. 
The description of the toothbrush is "It is purple with soft bristles. You used it just before Going to bed."

Check Taking the toothbrush:
	say "You brushed your teeth earlier.";
	Rule Fails.
	
Section 3 - Toothpaste

A toothpaste tube is a container in the medicine cabinet.
The printed name of the toothpaste tube is "toothpaste tube". 
Understand "paste" as the toothpaste. 
The description of the toothpaste tube is "'For sensitive teeth.'". 

Check Taking the toothpaste tube:
	say "You brushed your teeth earlier.";
	Rule Fails.

Part 5 - Laundry Room

The Laundry Room is a room. 
The Laundry Room is east of the white door. 
The Laundry Room is dark. 
The description is "This small laundry room is where you wash and dry your clothes, and store all the business that is associated with the task. A shelf spans the area above a front-loading washing machine and dryer[If the dryer-switch is Switched On]. They dryer is running.[End If]. A light switch on the wall is switched [if the laundry light switch is Switched On]on[otherwise]off[End If]. [paragraph break][If the sliding door is open]An open[otherwise]A closed[End If] sliding door to the north leads to the bathroom. [If the white door is open]An open[Otherwise]A closed[End If] white door to the west leads to the living room."
	
A shelf is a scenery supporter in the Laundry Room. 
The shelf is fixed in place.
	
A bottle of laundry detergent is on the shelf. 
The description is "It is an ordinary bottle of laundry detergent, original scent."
	
A bottle of fabric softener is on the shelf. 
The description is "You find the lavender scent of this fabric softener calming."

The white door is a scenery door. 
The white door is east of the Living Room. 
The white door is closed. 
Understand "laundry", "door", "laundry door", "laundry room door" as white door. 

The sliding door is a scenery door. 
The sliding door is north of the Laundry Room. 
The sliding door is closed.

A clothes basket is in the Laundry Room. "[If the clothes basket encloses nothing]An empty [Otherwise]A [End If]clothes basket rests on the floor in front of the dryer.[If the clothes basket encloses something] In the clothes basket [are] [a list of things enclosed by the clothes basket]." 
The clothes basket is the Player's holdall. 
The clothes basket is transparent. 
Understand "laundry basket" as the clothes basket. 
The clothes basket is unopenable.
	
Chapter 1 - Clothes Dryer

The clothes dryer is a scenery container in the Laundry Room. 
The clothes dryer is openable. 
The clothes dryer is closed. 
The clothes dryer is fixed in place. 
Understand "timer", "dryer door", "door" as the clothes dryer. 
The Description is "It is a standard [BeginHint]clothes dryer[EndHint]. According to the timer, a typical load takes 20 minutes to dry. Currently, the dryer door is [If the clothes dryer is open]open[Otherwise]closed[End If], and the clothes dryer is [If dryer-switch is Switched Off][BeginHint]not[EndHint] [End If]running."

The dryer-switch is a scenery device. 
The dryer-switch is part of the clothes dryer. 
The dryer-switch is Switched Off. 
The printed name of the dryer-switch is "clothes dryer".

Check Inserting something not wearable into the clothes dryer:
	Say "Better not. You might ruin [the noun].";
	Rule Fails.

Check Inserting something dry into the clothes dryer:
	say "You have no reason to put [the noun] in the dryer.";
	Rule Fails.
	
Check Inserting something wet into the clothes dryer:
	Continue the Action.

Section 1 - Drying Clothes (A Recurring Scene)
	
Dryer-Running is a recurring scene. Dryer-Running begins when the dryer-switch is Switched On.[or Dryer-Timer is 30.]
	
Dryer-Running ends when dryer-switch is Switched Off.[or Dryer-Timer is 0.]
		
Dryer-Timer is a number that varies. Dryer-Timer is 0.
	
Check Examining the clothes dryer when the dryer-switch is Switched On:
	say "The clothes dryer is running. It looks like [Dryer-Timer] minutes remain on the timer.";
	Rule Succeeds.

Check Switching On the clothes dryer:
	Try Switching On the dryer-switch;
	Rule Succeeds.
	
Check Switching Off the clothes dryer:
	Try Switching Off the dryer-switch;
	Rule Succeeds.
	
Check Switching Off the dryer-switch when the dryer-timer is greater than 0 and the dryer-switch is Switched On:
	say "The dryer is running. The timer says [dryer-timer] minutes are left. Maybe find something to occupy your time until it finishes.";
	Rule Fails.
	
Check Switching On the dryer-switch when the dryer-switch is Switched On:
	say "The clothes dryer is already running.";
	Rule Fails.
	
Check Switching On the dryer-switch when the clothes dryer is open:
	say "You need to [BeginHint]close the clothes dryer[EndHint] before you can switch it on.";
	Rule Succeeds.
	
Check Switching On the dryer-switch when the dryer-switch is Switched Off:
	Now the dryer-switch is Switched On;
	Now the Dryer-Timer is 20;
	say "You turn on the dryer. The timer says [dryer-timer] minutes are left.";
	Rule Succeeds.
	
Check Opening the clothes dryer when the dryer-timer is greater than 0 and the dryer-switch is Switched On:
	say "The dryer is running. The timer says [dryer-timer] minutes are left. Maybe find something to do?";
	Rule Fails.

Every Turn during Dryer-Running:
	if Dryer-Timer is 0:
		say "Ding! The Dryer is done!";
		now the dryer-switch is Switched Off;
		if the clothes dryer encloses your favorite comfy PJs:
			Now the printed name of your favorite comfy PJs is "your favorite comfy PJs";
		if the clothes dryer encloses the sweats:
			Now the printed name of the sweats is "your sweats";
		if the clothes dryer encloses your robe:
			Now the printed name of your cotton robe is "your cotton robe";
		if the clothes dryer encloses the towel:
			Now the printed name of the towel is "towel";
		Dry-the-Clothes;
	otherwise:
		decrease Dryer-Timer by 1.
	
To Dry-the-Clothes:
	Repeat with item running through things in the clothes dryer:
		Now item is dry.
	
Chapter 2 - Washing Machine

The washing machine is a scenery container in the Laundry Room. 
The washing machine is openable. 
The washing machine is closed. 
The washing machine is fixed in place. 
Understand "wash machine", "washer" as the washing machine. 
The description is "It is a typical seven-cycle washing machine. You just used it yesterday. Currently, the washing machine's door is [If the washing machine is open]open[Otherwise]closed[End If]."

Check Inserting something into the washing machine:
	If the noun is wearable:
		Continue the Action;
	otherwise:
		say "You would wreck [the noun].";
	Rule Fails.
	
Check Switching On the washing machine:
	Let T be the Time of Day;
	Increase T by one minute;
	say "It is [T]. There are better times to do laundry.";
	Rule Fails.
	
Chapter 3 - Laundry Light Switch

The laundry light switch is a device in the Laundry Room.
The laundry light switch is scenery.
Understand "light", "laundry room light switch" as the laundry light switch.

Check Switching On the laundry light switch:
	Now the laundry light switch is lit;
	Continue the Action.
	
Check Switching Off the laundry light switch:
	Now the laundry light switch is unlit;
	Continue the Action.
	
Part 6 - Kitchen

The Kitchen is a room. 
The Kitchen is south of the Living Room. 
The Kitchen is dark. 
The description is "The kitchen is where you store, prepare, and cook your food. In the kitchen is an electric range, [BeginHint]a refrigerator[EndHint], a microwave, a sink, [BeginHint]a panTry[EndHint], some [BeginHint]drawers[EndHint], and a curtained window overlooking the street below. A light switch on the wall is switched [If the kitchen light switch is Switched On]on[otherwise]off[End If].[Paragraph Break]To the north lies the living room. To the south, the door Exiting your apartment is closed."

The apartment door is in the Kitchen. 
The apartment door is scenery.

Check Going from the Kitchen when the refrigerator is open:
	Say "Are you just Going to leave the refrigerator door open like that?";
	Rule Fails.
		
Check Going from the Kitchen when the oven is open:
	Say "Are you just Going to leave the oven door open like that?";
	Rule Fails.
	
Check Going from the Kitchen when the microwave is open:
	Say "Are you just Going to leave the microwave door open like that?";
	Rule Fails.

Check unlocking the apartment door with something:
	Try Opening the apartment door;
	Rule Fails.
	
Check Opening the apartment door when the kitchen light switch is lit:
	Say "You have no good reason to leave your apartment in the middle of the night.";
	Rule Fails.
	
Check Going south when the Player is in the Kitchen and the kitchen light switch is lit:
	Try Opening the apartment door;
	Rule Fails.

A microwave is an appliance in the Kitchen.
The bulk capacity of the microwave is 3.

Chapter 1 - Electric Range
	
A electric range is an appliance in the kitchen.
Understand "stove" as the electric range.
The description is "This standard electric range is one of your apartment's included appliances. It includes a built-in cooktop and oven."
			
Check Opening the electric range:
	Try Opening the oven;
	Rule Succeeds.
			
Check Closing the electric range:
	Try Closing the oven;
	Rule Succeeds.

The cooktop is an appliance in the kitchen. The cooktop is unopenable. The description is "It is an electric, four-burner cooktop. It is currently turned off."
				
The oven is an appliance in the kitchen.
Understand "door" as the oven.
The description is "Currently, the oven is turned off and the door is [If the oven is open]open[Otherwise]closed[End If].[If the oven is open] Inside the oven is [a list of things enclosed by the oven]."
	
Chapter 2 - Kitchen Light Switch

The kitchen light switch is a device in the Kitchen. 
The kitchen light switch is scenery. 
Understand "light" as the kitchen light switch.

Check Switching On the kitchen light switch:
	Now the kitchen light switch is lit;
	Continue the Action.
	
Check Switching Off the kitchen light switch:
	Now the kitchen light switch is unlit;
	Continue the Action.
	
Chapter 3 - Refrigerator

The refrigerator is a scenery container in the kitchen.
The refrigerator is openable. 
The refrigerator is closed. 
The refrigerator is fixed in place. 
Understand "fridge", "frige", "icebox", "door", "freezer" as the refrigerator. 
The description is "It is a standard side-by-side refrigerator. You keep most of your perishable [BeginHint]foodstuff[EndHint] in it. Right now, the refrigerator door is [If the refrigerator is open]open[Otherwise]closed[End If]."

Check Opening the refrigerator:
	Now the refrigerator is lit;
	Continue the Action.

Check Closing the refrigerator:
	Now the refrigerator is unlit;
	Continue the Action.

Check Searching the refrigerator:
	Try Searching the foodstuff;
	Rule Succeeds.

Section 1 - Jelly

Some foodstuff is in the refrigerator. 
Understand "food" as the foodstuff. 
The description is "The foodstuff consists of an assortment of refrigerated foods[If the jelly is in the refrigerator], including some jelly[End If][If Hints-On is True and the jelly is nowhere].[BeginHint] If you search the foodstuff, you might find something to eat[EndHint][End If]."

Check Taking the foodstuff:
	Try Searching the foodstuff;
	If the jelly is in the refrigerator:
		Try Taking the jelly;
	Otherwise:
		Rule Fails;
	Rule Succeeds.

A jelly is nowhere. 
The indefinite article of the jelly is "some". 
Understand "jar", "jam" as the jelly.
The description is "It is a jar full of your favorite grape jelly."
		
Check Opening the jelly:
	Say "You can just [BeginHint]spread the jelly on something[EndHint] if you want to.";
	Rule Fails.
	
Check Examining the foodstuff for the first time:
	Try Searching the foodstuff;
	Rule Succeeds.

Check Searching the foodstuff when the jelly is nowhere:
	Say "You search the foodstuff and find some [BeginHint]jelly[EndHint]!";
	Now the jelly is in the refrigerator;
	Rule Succeeds.

Check Searching the foodstuff:
	Say "There is a lot of food here, but nothing looks more delicious than the [BeginHint]jelly[EndHint][If the jelly is handled] you already found[End If].";
	Rule Succeeds.
	
Check Dropping the jelly:
	Say "If you don't [BeginHint]put the jelly in the refrigerator[EndHint], it will go bad.";
	Rule Fails.
	
Check Inserting the jelly into something:
	If the second noun is the refrigerator: 
		Continue the Action;
	Otherwise:
		Say "If you don't [BeginHint]put the jelly in the refrigerator[EndHint], it will go bad.";
	Rule Fails.
	
A b#it is nowhere. 
A b#it is wearable. 
The printed name of the b#it is "grape jelly on your fingers". 
The indefinite article of the b#it is "some". 
The description of the b#it is "Your fingers are covered in grape jelly."

Check Tasting the jelly:
	Try Eating the jelly;
	Rule Succeeds.
	
Check Eating the jelly when the Player does not enclose the jelly:
	Say "You aren't holding the jelly.";
	Rule Fails.
	
Check Eating the jelly when the Player encloses the butter knife and the Player encloses the jelly and the Player does not wear the b#it:
	Say "You scoop out some jelly with the butter knife and taste it. Not bad[If Player-Hungry is True], but it does nothing for your hunger[End If].";
	Now Knife-Dirty is True;
	Rule Succeeds.

Check Eating the jelly when the Player does not enclose the butter knife and the Player does not carry the b#it and the Player encloses the jelly:
	Say "You scoop out some jelly with your fingers and eat it. Now, your fingers are covered in jelly[If Player-Hungry is True], and it does nothing for your hunger[End If].";
	Now the Player carries the b#it;
	Now Player-Dirty is True;
	Rule Succeeds.
	
Check Eating the jelly when the Player wears the b#it:
	Say "Your fingers are already covered in jelly. Maybe you should [BeginHint]wash up[EndHint].";
	Rule Fails.
	
Check Putting the jelly on the bread when the Player does not enclose the knife:
	If the bread is jellied:
		say "You already put jelly on the bread.";
	Otherwise:
		If the Player does not enclose the jelly:
			say "You aren't holding the jelly.";
			Rule Fails;
		Otherwise:
			If the Player does not enclose the bread:
				say "You aren't holding the bread.";
				Rule Fails;
			Otherwise:
				Now Player-Dirty is True;
				Now the Player carries the b#it;
				Say "Using your fingers, [Run paragraph on]";
				Spread-the-Jelly;
				Rule Succeeds.

Check Putting the jelly on the bread when the Player encloses the butter knife:
	If the bread is jellied:
		say "You already put jelly on the bread.";
	Otherwise:
		If the Player does not enclose the jelly:
			say "You aren't holding the jelly.";
			Rule Fails;
		Otherwise:
			If the Player does not enclose the bread:
				say "You aren't holding the bread.";
				Rule Fails;
			Otherwise:
				Say "Using the butter knife, [Run Paragraph On]";
				Spread-the-Jelly;
				Rule Succeeds.
	
To Spread-the-Jelly:
	Say "you spread some jelly on the bread.[paragraph break]"; 
	Now the bread is jellied;
	Now Knife-Dirty is True;
	If the bread is buttered:
		Now the Player carries a PBJ sandwich;
		Now the bread is nowhere;
		Now the printed name of the butter knife is "butter knife smeared with peanut butter and jelly";
		say "Congratulations, you made a [BeginHint]peanut butter and jelly sandwich[EndHint]!";
	Otherwise:
		Now the printed name of the bread is "jellied bread";
		Now the printed name of the butter knife is "butter knife smeared with jelly";
	Rule Succeeds.
	
Chapter 4 - PanTry
	
The panTry is a scenery container in the kitchen. 
The panTry is fixed in place. 
The panTry is openable. 
The panTry is closed. 
Understand "door", "panTry door" as the panTry. 
The description is "Your panTry is a cupboard in which you keep your non-refrigerated [BeginHint]dry goods[EndHint] and canned goods. The panTry door is currently [If the panTry is open]open[Otherwise]closed[End If]."
	

Section 1 - Creamy Peanut Butter

Some dry goods are in the panTry. 
The description is "You see what is left of an assortment of mostly nonperishable dry goods you stocked up on a while ago[If the peanut butter is in the panTry], including some peanut butter[End If][If Hints-On is True].[BeginHint] If you search the dry goods, you might find something to eat[EndHint][End If]."

Check Taking the dry goods:
	Try Searching the dry goods;
	If the peanut butter is in the panTry:
		Try Taking the peanut butter;
	Otherwise:
		Rule Fails;
	Rule Succeeds.
	
Check Examining the dry goods for the first time:
	Try Searching the dry goods;
	Rule Succeeds.

The peanut butter is nowhere. 
The indefinite article of the peanut butter is "some".
Understand "jar", "pb" as the peanut butter. 
The description of the peanut butter is "It is a jar of your favorite creamy [BeginHint]peanut butter[EndHint]."

Check Opening the peanut butter:
	Say "You can just [BeginHint]spread the peanut butter on something[EndHint] if you want to.";
	Rule Fails.
	
Check Searching the dry goods when the peanut butter is nowhere:
	Say "You find some [BeginHint]peanut butter[EndHint] in the dry goods!";
	Now the peanut butter is in the panTry;
	Rule Succeeds.
	
Check Searching the dry goods:
	Say "There is a lot of food here, but nothing looks more delicious than the [BeginHint]peanut butter[EndHint][If the peanut butter is handled] you already found[End If].";
	Rule Succeeds.
	
A dol#lop is nowhere. 
A dol#lop is wearable. 
The printed name of the dol#lop is "creamy peanut butter on your fingers". 
The indefinite article of the dol#lop is "some". 
The description of the dol#lop is "Your fingers are covered in peanut butter."

Check Tasting the peanut butter:
	Try Eating the peanut butter;
	Rule Succeeds.
	
Check Eating the peanut butter when the Player does not enclose the peanut butter:
	Say "You aren't holding the peanut butter.";
	Rule Fails.
	
Check Eating the peanut butter when the Player encloses the butter knife and the Player encloses the peanut butter and the Player does not wear the dol#lop:
	Say "You scoop out some peanut butter with the butter knife and taste it. Not bad[If Player-Hungry is True], but it does nothing for your hunger[End If].";
	Now Knife-Dirty is True;
	Rule Succeeds.
	
Check Eating the peanut butter when the Player does not enclose the butter knife and the Player does not carry the dol#lop and the Player encloses the peanut butter:
	Say "You scoop out some peanut butter with your fingers and eat it. Now, your fingers are covered in peanut butter[If Player-Hungry is True], and it does nothing for your hunger[End If].";
	Now the Player carries the dol#lop;
	Now Player-Dirty is True;
	Rule Succeeds.
	
Check Eating the peanut butter when the Player wears the dol#lop:
	Say "Your fingers are already covered in peanut butter. Maybe you should get cleaned up.";
	Rule Fails.

Check Putting the peanut butter on the bread when the Player does not enclose the knife:
	If the bread is buttered:
		say "You already put peanut butter on the bread.";
	Otherwise:
		If the Player does not enclose the peanut butter:
			say "You aren't holding the peanut butter.";
			Rule Fails;
		Otherwise:
			If the Player does not enclose the bread:
				say "You aren't holding the bread.";
				Rule Fails;
			Otherwise:
				Now Player-Dirty is True;
				Now the Player carries the dol#lop;
				Say "Using your fingers, [Run paragraph on]";
				Spread-the-PB;
				Rule Succeeds.

Check Putting the peanut butter on the bread when the Player encloses the butter knife:
	If the bread is buttered:
		say "You already put peanut butter on the bread.";
	Otherwise:
		If the Player does not enclose the peanut butter:
			say "You aren't holding the peanut butter.";
			Rule Fails;
		Otherwise:
			If the Player does not enclose the bread:
				say "You aren't holding the bread.";
				Rule Fails;
			Otherwise:
				Say "Using the butter knife, [Run Paragraph On]";
				Spread-the-PB;
				Rule Succeeds.
	
To Spread-the-PB:
	Say "you spread some peanut butter on the bread.[paragraph break]"; 
	Now the bread is buttered;
	Now Knife-Dirty is True;
	If the bread is jellied:
		Now the Player carries a PBJ sandwich;
		Now the bread is nowhere;
		Now the printed name of the butter knife is "butter knife smeared with peanut butter and jelly";
		say "Congratulations, you made a [BeginHint]peanut butter and jelly sandwich[EndHint]!";
	Otherwise:
		Now the printed name of the bread is "peanut-buttered bread";
		Now the printed name of the butter knife is "butter knife smeared with peanut butter";
	Rule Succeeds.

Section 2 - Bread

A breadbox is a container in the kitchen. "A [BeginHint]breadbox[EndHint] is on the counter." 
The bulk of the breadbox is 4.
The breadbox is openable. 
The breadbox is closed. 
Understand "lid", "bread box", "box" as the breadbox. 
The description is "It is a vintage stainless steel bread box. The [BeginHint]lid[EndHint] is currently [If the breadbox is open]open[Otherwise]closed."

Some bread is in the breadbox. 
Some bread is singular-named.
The indefinite article of the bread is "some".
Some bread is edible. 
Some bread can be buttered or unbuttered. 
Some bread is unbuttered.
Some bread can be jellied or unjellied. 
Some bread is unjellied.
Understand "slices","two slices of bread","sliced bread", "jellied bread", "peanut-buttered bread", "buttered bread" as some bread.
The description is "These are your last [BeginHint]two slices of bread[EndHint][If the bread is jellied]. Both slices have jelly on them[End If][If the bread is buttered]. Both slices have peanut butter on them[End If]." 

A PBJ sandwich is nowhere. 
A PBJ sandwich is edible. 
Understand "peanut butter and jelly sandwich","pb and jelly sandwich" as the PBJ sandwich.
The description is "It is a beautiful peanut butter and jelly sandwich!"

Check Eating the bread for the first time:
	Say "Plain bread doesn't sound very appetizing, and these are the only two slices you have left.";
	Rule Fails.
	
Check Eating the bread:
	Say "You eat the bread. Not bad, but now you have no bread left, and you are still hungry.";
	End the story finally saying "You are never Going back to sleep tonight."
	
Check Eating the jellied bread for the first time:
	Say "A jelly sandwich doesn't sound like it would be very filling, and these are the only two slices of bread you have left. Your sandwich needs something else...hmm.";
	Rule Fails.
	
Check Eating the jellied bread:
	Say "You eat the jelly sandwich. Not bad, but now you have no bread left, and you are still hungry.";
	End the story finally saying "You are never Going back to sleep tonight."
	
Check Eating the buttered bread for the first time:
	Say "A peanut-butter sandwich doesn't sound like it would be very filling, and these are the only two slices of bread you have left. Your sandwich needs something else...hmm.";
	Rule Fails.
	
Check Eating the buttered bread:
	Say "You eat the peanut-butter sandwich. Not bad, but now you have no bread left, and you are still hungry.";
	End the story finally saying "You are never Going back to sleep tonight."
	

		
Chapter 5 - Kitchen Drawer

The kitchen drawer is a scenery container in the kitchen. 
The kitchen drawer is openable. 
The kitchen drawer is closed. 
Understand "handle","drawers" as the kitchen drawer.
	
Check Pulling the kitchen drawer:
	Try Opening the kitchen drawer;
	Rule Succeeds.
	
Check Pushing the kitchen drawer:
	Try Closing the kitchen drawer;
	Rule Succeeds.

A butter knife is nowhere. 
The description is "This [If the bread is jellied]dirty [Otherwise if the bread is buttered][BeginHint]dirty[EndHint] [End If]butter knife is perfect for spreading butters and jams."
	
A flatware is in the kitchen drawer. 
The indefinite article of the flatware is "some". 
The flatware is fixed in place. 
Understand "utensils", "knife", "spoon", "fork" as a flatware.
The description is "The flatware consists of many different utensils[If the butter knife is in the kitchen drawer], including a butter knife[End If][If Hints-On is True].[BeginHint] If you are looking for a specific utensil in the flatware, you will need to search the flatware[EndHint][End If]."

Check Taking the flatware:
	Try Searching the flatware;
	If the butter knife is in the kitchen drawer:
		Try Taking the butter knife;
	Otherwise:
		Rule Fails;
	Rule Succeeds.
	
Check Examining the flatware for the first time:
	Try Searching the flatware;
	Rule Succeeds.
	
Check Searching the flatware when the butter knife is nowhere:
	Say "You find a [BeginHint]butter knife[EndHint] among the flatware.";
	Now the butter knife is in the kitchen drawer;
	Rule Succeeds.
			
Check Searching the flatware:
	Say "There is a lot of flatware here, but nothing looks more interesting than the [BeginHint]butter knife[EndHint][If the butter knife is handled] you already found[End If].";
	Rule Succeeds.
	
Check Dropping the butter knife when the Player encloses the butter knife and Knife-Dirty is True:
	Say "Gross! Why would you leave a [BeginHint]dirty[EndHint] butter knife just lying around?";
	Rule Fails.
	
Check Putting the butter knife on something when the Player encloses the butter knife and Knife-Dirty is True:
	Say "Gross! Why would you put a [BeginHint]dirty[EndHint] butter knife on [the second noun]?";
	Rule Fails.
	
Check Inserting the butter knife into something when the Player encloses the butter knife and Knife-Dirty is True:
	If the second noun is the jelly:
		Continue the Action;
	Otherwise:
		If the second noun is the peanut butter:
			Continue the Action;
		Otherwise:
			Say "Gross! Why would you put a [BeginHint]dirty[EndHint] butter knife in [the second noun]?";
	Rule Fails.
	
Check Inserting the butter knife into the kitchen-sink when the Player encloses the butter knife and Knife-Dirty is True:
	Say "You put the dirty knife into the kitchen sink with the other dirty dishes.";
	Now the butter knife is in the kitchen-sink;
	Rule Succeeds.
	
Chapter 6 - Kitchen Sink

The kitchen-sink is a scenery container in the Kitchen.
The printed name of the kitchen-sink is "kitchen sink".
Understand "sink", "faucets","faucet","kitchen faucet","kitchen faucets","water" as the kitchen-sink.
The description is "It is just an ordinary kitchen sink...and, apparently, your own personal [BeginHint]dirty-dish storage[EndHint] basin."

Some dirty dishes are in the kitchen-sink. 
Understand "pile of dishes", "pile of dirty dishes", "pile" as the dirty dishes. 
The description is "The sink is full of [BeginHint]dirty[EndHint] dishes and flatware from the past few days. Looks like you will be doing dishes tomorrow."

Check Taking the dirty dishes:
	Say "It is the middle of the night. You can wait until tomorrow to do dishes.";
	Rule Fails.

Check Switching On the kitchen-sink:
	Try Taking the dirty dishes;
	Rule Fails.
	
	
Part 7 - Windows

A looking-glass is a kind of container. 
A looking-glass is always fixed in place. 
A looking-glass is always transparent.

A window is a kind of looking-glass. 
A window is never openable.

A window-covering is a kind of container. 
A window-covering is always fixed in place. 
A window-covering is openable. 
A window-covering is always closed. 

Some curtains are a kind of window-covering. 
Some curtains are never openable. 
Understand "blinds", "drapes","draperies" as the curtains. 
The description is "You handpicked these perfectly-crafted, room-darkening, pre-cut curtains yourself."

Check Opening the curtains:
	Say "You don't have to open the curtains if you want to look out the window.";
	Rule Fails.

In Your Bedroom is a scenery window.
In Your Bedroom are some scenery curtains.

In the Living Room is a scenery window.
In the Living Room are some scenery curtains.

In the Kitchen is a scenery window.
In the Kitchen are some scenery curtains.

Check Searching a window:
	Try Examining the noun;
	Rule Succeeds.
	
Check Opening a window:
	Say "You have no reason to open the window.";
	Rule Fails.
	
Check Closing a window:
	Say "The window is already closed.";
	Rule Fails.
	
Check Examining a window when the Player is in your bed:
	Say "You will have to get out of bed before you can look out the window.";
	Rule Fails.
	
Check Examining a window when the Turn Count is less than 240:
	Say "You peek behind the curtains and look out the window. Three stories below you lies a dark and quiet street. No one is on the sidewalks, and there is barely a moving car in sight. Not much is happening in the city this time of night.";
	Rule Succeeds.
	
Check Examining a window when the the Turn Count is greater than 239 and the Turn Count is less than 361:
	Say "It is starting to get light outside. How long have you been awake?";
	Rule Succeeds.
	
Check Examining a window when the Turn Count is greater than 360 and the Turn Count is less than 720:
	End the story finally saying "It is light out. It is literally the next morning. You might as well just stay up."
	
Check Examining a window when the Turn Count is greater than 719:
	End the story finally saying "Seriously? Look at the time. 	What's the point?"
	
Part 8 - Listening and Smelling

Check Listening when the shower-faucets are Switched Off and the sink faucets are Switched Off and the TV is Switched Off:
	Say "You hear nothing unexpected.";
	Rule Succeeds.

Check Listening:
	If the shower-faucets are Switched On:
		Say "You hear the shower running[If the Player is not in the En Suite Bathroom] in the bathroom[End If].";
	If the sink faucets are Switched On:
		Say "You hear the sink running[If the Player is not in the En Suite Bathroom] in the bathroom[End If].";
	If the TV is Switched On:
		Say "You hear the TV playing[If the Player is not in the Living Room] in the living room[End If].";
	Rule Succeeds.
	
Check Smelling when the Player is in the Kitchen:
	Say "A sour funk is beginning to rise from the sink. You had better wash those dishes tomorrow.";
	Rule Succeeds.
	
Check Smelling when the Player is in the En Suite Bathroom:
	Say "The sweet honeysuckle scent of yesterday's shower bomb still lingers.";
	Rule Succeeds.
	
Part 9 - Drinking

Some water is nowhere.
Some water is scenery.

Check Drinking some water when the sink faucets are Switched On and the Player is in the en suite bathroom:
	Say "You drink the water.";
	Rule Succeeds.

Check Drinking some water when the shower-faucets are Switched On and the Player is in the shower:
	Say "You drink the water.";
	Rule Succeeds.

Check Drinking some water when the shower-faucets are Switched On and sink faucets are Switched Off and the Player is in the en suite bathroom:
	Say "You would have to get in the shower.";
	Rule Succeeds.

Check Drinking some water when the shower-faucets are Switched Off and the Player is in the shower:
	Say "You would have to turn on the shower.";
	Rule Succeeds.

[Check Drinking some water when some water is in the en suite bathroom and the Player is in the en suite bathroom:
	Say "You drink the water.";
	Rule Succeeds.

Check Drinking some water when some water is in the shower and the Player is in the en suite bathroom:
	Say "You would have to get into the shower.";
	Rule Succeeds.

Check Drinking some water when some water is in the en suite bathroom and the Player is in the shower:
	Say "You would have to get out of the shower.";
	Rule Succeeds.]

Check Drinking the detergent:
	Its-poison;
	Rule Fails.
	
Check Drinking the fabric softener:
	Its-poison;
	Rule Fails.
	
To Its-Poison:
	Say "[The noun] is poisonous if consumed in large quantities. It shouldn't be drunk.".
	
Book 2 - Winning the Game

Check Eating the PBJ sandwich:
	Say "You eat the peanut butter and jelly sandwich. Yum! That satisfied your hunger, and you should have no trouble Going back to sleep now.";
	Now the PBJ sandwich is nowhere;
	Now Player-Hungry is False;
	Rule Succeeds.

Check Entering your bed when Player-Hungry is False:
	If the table lamp is lit:
		say "Wait a minute. Did you leave the table lamp on in the living room?";
		Rule Fails;
	Otherwise:
		If the bathroom light switch is lit:
			say "Wait a minute. Did you leave the light on in the bathroom?";
			Rule Fails;
		Otherwise:
			If the laundry light switch is lit:
				say "Wait a minute. Did you leave the light on in the laundry room?";
				Rule Fails;
			Otherwise:
				If the kitchen light switch is lit:
					say "Wait a minute. Did you leave the light on in the kitchen?";
					Rule Fails;
				Otherwise:
					If the TV is Switched On:
						Say "Wait a minute. Do you hear the TV?";
						Rule Fails;
					Otherwise:
						If the shower-faucets are Switched On:
							Say "Wait a minute. Do you hear water running?";
							Rule Fails;
						Otherwise:
							If the sink faucets are Switched On:
								Say "Wait a minute. Do you hear water running?";
								Rule Fails;
							Otherwise:
								If the Player is wet:
									Say "If you crawl into bed before you dry off, you will get your sheets wet, and you will never fall back asleep.";
									Rule Fails;
								Otherwise:
									If the bedroom lamp is lit:
										Continue the Action;
										Rule Succeeds;
									Otherwise:
										say "You climb into bed and fall asleep.";
										Win-the-Game;
										Rule Succeeds.

Check Switching Off the lamp when the Player is in your bed and Player-Hungry is False:
	say "You switch off the lamp and fall asleep.";
	Win-the-Game;
	Rule Succeeds.

To Win-the-Game:
	If the Turn Count is less than 33:
		End the story finally saying "You didn't waste any time getting your snack and getting back to bed. You achieved the highest possible rank of Nosher!";
	Otherwise:
		If the Turn Count is greater than 32 and the Turn Count is less than 121:
			End the story finally saying "Not bad, but a few unnecessary delays getting back to bed has resulted in the rank of Laggard.";
		Otherwise:
			If the Turn Count is greater than 120:
				End the story finally saying "You got your snack, but with all the time it took, you achieved the unfortunate rank of Insomniac."
				
Volume 5 - Solution and Test

Test solution with "GET OUT OF BED / TURN ON LIGHT / S / TURN ON LIGHT / S / TURN ON LIGHT / OPEN BREADBOX / OPEN FRIDGE / OPEN PANTRY / SEARCH FOODSTUFF / SEARCH DRY GOODS / GET JELLY / GET PB / GET BREAD / OPEN DRAWER / SEARCH FLATWARE / GET BUTTER KNIFE / SPREAD JELLY ON BREAD / SPREAD PB ON BREAD / EAT SANDWICH / PUT JELLY IN FRIDGE / PUT PB IN PANTRY / PUT KNIFE IN SINK / CLOSE REFRIGERATOR / TURN OFF LIGHT / N / TURN OFF LIGHT / N / GET IN BED / TURN OFF LAMP".

Volume 6 - Release Updates

[Release 5 (2023-01-01): First production Release]

[Release 6 (2023-01-07): Updated Help, changed genre to Slice of Life, fixed grammar, simplified showering, fixed max items carried, added additional ways to spread the jelly and peanut butter on the bread]

[Release 7 (2023-01-24): Added listening and Smelling; Taking foodstuff, flatware, and dry goods now searches and takes jelly, butter knife, and PB; butter knife now understood to be flatware and is searched when taken; set bulk capacity of microwave to 3 and bulk of breadbox to 4; detergent and fabric softener now poisonous; water in the bathroom is drinkable; Examining foodstuff, flatware, and dry goods is now the same as Searching, allows Player to eat jellied or peanut-buttered bread with a customized warning first]

[Release 8 [2023-02-05]: Grammar corrections; added cover art; added default Listen; capitalized Actions]